{-# LANGUAGE MultilineStrings #-}
{-# LANGUAGE OverloadedStrings #-}

module Templates where

import BlazeSupport
import Constants
import Control.Monad (forM_, unless, when)
import Data.ByteString.Lazy
import Data.Maybe
import Hakyll
import Text.Blaze.Html5 as H
import Text.Blaze.Html5.Attributes as A
import Utils

data PageMetadata = PageMetadata
  { title :: Maybe String
  , pagetitle :: Maybe String
  , slug :: Maybe String
  , description :: Maybe String
  , thumbnail :: Maybe String
  , url :: Maybe String
  }

pageHead ::
  PageMetadata ->
  Html
pageHead (PageMetadata{title, pagetitle, slug, description, thumbnail, url}) = do
  let description' =
        toValue $
          fromMaybe
            siteDescription
            description
  let thumbnail' =
        toValue $
          fromMaybe
            "/favicon.svg"
            thumbnail
  let title' = case pagetitle of
        Just pagetitle' -> pagetitle'
        Nothing -> case slug of
          Just slug' -> slug'
          Nothing -> siteName ++ " >> " ++ fromMaybe "Lost" title
  let url' = toValue $ fromMaybe siteRoot url
  H.head $
    do
      meta ! charset "utf-8"
      meta ! httpEquiv "x-ua-compatible" ! content "ie=edge"
      meta ! name "viewport" ! content "width=device-width, initial-scale=1"
      H.title $ toHtml title'
      meta ! name "og:title" ! (content . toValue) title'
      meta ! name "og:site_name" ! content (toValue siteName)
      meta
        ! name "og:description"
        ! content description'
      meta
        ! name "description"
        ! content description'
      meta ! name "og:image" ! content thumbnail'
      meta ! name "og:type" ! content "website"
      meta ! name "og:url" ! content url'
      meta ! name "robots" ! content "index, follow"
      link ! rel "stylesheet" ! href "/css/main.css"
      link ! rel "icon" ! type_ "image/png" ! href "/favicon-96x96.png" ! sizes "96x96"
      link ! rel "icon" ! type_ "image/svg+xml" ! href "/favicon.svg"
      link ! rel "apple-touch-icon" ! sizes "180x180" ! href "/apple-touch-icon.png"

pageFooter :: String -> String -> String -> Html
pageFooter commit ghc time = footer ! class_ "border-t mt-8 border-subtle pt-4 pb-8 text-sm text-subtle" $ do
  p $ do
    "Generated by "
    a ! class_ "text-link" ! href "https://jaspervdj.be/hakyll/" $ "Hakyll"
    " with GHC "
    toHtml ghc
    " at "
    toHtml time

defaultTemplate :: Context String -> Item String -> Compiler Html
defaultTemplate ctx item =
  do
    title <- getField' "title"
    author <- getField' "author"
    date <- getField' "date"
    pagetitle <- getField' "pagetitle"
    location <- getField' "location"
    slug <- getField' "slug"
    enableComments' <- getField' "enable-comments"
    ghc <- getField' "ghc-version"
    time <- getField' "last-commit-timestamp"
    commitHash <- getField' "commit-hash"
    url <- getField' "url"
    thumbnail <- getField' "thumbnail"
    description <- getField' "description"
    let ghc' = fromMaybe "GHC_VER_PLACEHOLDER" ghc
    let commitHash' = fromMaybe "COMMIT_HASH_PLACEHOLDER" commitHash
    let time' = fromMaybe "TIME_PLACEHOLDER" time
    return $ docTypeHtml ! lang "en" $ do
      pageHead PageMetadata{title, pagetitle, slug, thumbnail, description, url}
      body
        ! class_ "mx-auto max-w-2xl px-4 py-8"
        $ do
          main $ do
            h1 ! class_ "text-2xl font-bold mb-2" $ forM_ title toHtml
            H.div ! class_ "text-subtle mb-6" $ do
              forM_ date $ \date' -> p $ toHtml date'
              forM_ author $ \author' -> p $ em "by " >> toHtml author'
            H.div ! class_ "prose" $ preEscapedToHtml (itemBody item)
            pageFooter commitHash' ghc' time'
 where
  getField' = getStringField ctx item

postListItem :: Context t -> Item t -> Compiler Html
postListItem ctx item = do
  title <- getField' "title"
  description <- getField' "description"
  url <- getField' "url"
  date <- getField' "date"
  pure
    $ li ! class_ "mb-4"
    $ a
      ! href (toValue $ fromMaybe "#" url)
      ! class_ "block hover:bg-gray-100 p-2 -mx-2 rounded"
    $ do
      H.div ! class_ "flex justify-between items-baseline gap-4" $ do
        H.span ! class_ "font-medium" $ toHtml $ fromMaybe "Untitled" title
        forM_ date $ \d -> H.span ! class_ "text-subtle text-sm" $ toHtml d
      forM_ description $ \desc ->
        p ! class_ "text-subtle text-sm mt-1" $ toHtml desc
 where
  getField' = getStringField ctx item

archivePage :: Context String -> Item String -> Compiler Html
archivePage ctx item = do
  let getList' = getList ctx item
  ListData innerCtx posts <- getList' "posts"
  sortedPosts <- recentFirst posts
  postsRendered <- mapM (postListItem innerCtx) sortedPosts
  pure $ do
    p ! class_ "mb-4" $ do
      "All posts, sorted by date. Subscribe via "
      a ! href "/feed.xml" $ "RSS"
      " or "
      a ! href "/atom.xml" $ "Atom"
      "."
    ul ! class_ "list-none p-0" $ mconcat postsRendered

indexPage :: Context String -> Item String -> Compiler Html
indexPage ctx item = do
  let getList' = getList ctx item
  ListData innerCtx posts <- getList' "posts"
  sortedPosts <- Prelude.take 5 <$> recentFirst posts
  postsRendered <- mapM (postListItem innerCtx) sortedPosts
  pure $ do
    H.div ! class_ "mb-8" $ do
      p "Welcome. This is a static site template powered by Hakyll and Typst."
      p $ do
        "See the "
        a ! href "/about" $ "about page"
        " for more information."
    unless (Prelude.null sortedPosts) $ H.div $ do
      h2 ! class_ "text-xl font-bold mb-4" $ "Recent Posts"
      ul ! class_ "list-none p-0" $ mconcat postsRendered
      p ! class_ "mt-4" $ a ! href "/archive" $ "View all posts â†’"
